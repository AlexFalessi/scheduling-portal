subprojects {
    apply plugin: 'java'
    apply plugin: 'jetty'
    apply plugin: 'maven'

    group = 'org.ow2.proactive_grid_cloud_portal'
    version = schedulingPortalVersion

    repositories {
        if (project.hasProperty('local')) mavenLocal()
        mavenCentral()
        maven { url 'http://repository.sonatype.org/content/groups/forge/' }
        maven { url 'http://repo.activeeon.com/content/repositories/thirdparty/' }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://repo.activeeon.com/content/repositories/snapshots/") {
                    authentication(userName: "${System.getProperty('nexusUsername')}",
                            password: "${System.getProperty('nexusPassword')}")
                }
            }
        }
    }

    dependencies {
        providedCompile 'com.google.gwt:gwt-user:2.5.1'
        providedCompile 'com.google.gwt:gwt-dev:2.5.1'
        providedCompile 'com.smartgwt:smartgwt:4.0d-2014-02-26' // SNAPSHOT build to fix PORTAL-356
        providedCompile 'com.google.gwt.google-apis:gwt-visualization:1.1.1'
        // Needed for GWT compile and at runtime for RequestBuilder
        // Specify two artifacts as workaround for GRADLE-1934
        compile('javax.validation:validation-api:1.0.0.GA')
        compile('javax.validation:validation-api:1.0.0.GA:sources')
        compile 'org.slf4j:slf4j-api:1.5.8'
        compile 'commons-httpclient:commons-httpclient:3.1'
        compile 'commons-io:commons-io:1.3.1'
        compile 'commons-fileupload:commons-fileupload:1.2'
        compile 'org.apache.httpcomponents:httpclient:4.2.3'

        compile 'org.jboss.resteasy:resteasy-jaxrs:2.3.3.Final'

        runtime 'com.google.gwt:gwt-servlet:2.5.1'

        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
    }

    jar { // build jar with sources for GWT
        from sourceSets.main.allSource
    }

}

project(':applet') {
    task visu(type: Jar) {
        from sourceSets.main.output
        exclude '**/DataServerLauncher*'
        archiveName = "visu.jar"
        manifest.attributes("Main-Class": "org.ow2.proactive_grid_cloud_portal.extra.RemoteViewer")
    }

    task dataserver(type: Jar) {
        from sourceSets.main.output
        exclude '**/RemoteViewer*'
        archiveName = "servers.jar"
        manifest.attributes("Main-Class": "org.ow2.proactive_grid_cloud_portal.extra.DataServerLauncher")
    }

    build.dependsOn visu, dataserver
}

project(':rm-portal') {
    dependencies {
        compile 'codemirror:codemirror2-gwt:1.2.0'

        runtime 'net.bull.javamelody:javamelody-core:1.33.0'
        runtime 'org.slf4j:slf4j-simple:1.5.8'

        compile project(':common')
    }
    war {
        def replaceTokens = {
            it.filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['portal_version': project.version])
        }
        filesMatching('**/portal.html', replaceTokens)
        filesMatching('**/rm.conf', replaceTokens)
    }
}

project(':scheduler-portal') {
    dependencies {
        compile 'raphaelgwt:raphaelgwt:r28'
        compile 'org.vectomatic:lib-gwt-file:0.3.3'
        compile 'org.codehaus.jettison:jettison:1.2'
        compile 'org.jboss.resteasy:resteasy-multipart-provider:2.3.3.Final'

        compile project(':common')

        runtime 'net.bull.javamelody:javamelody-core:1.33.0'
        runtime 'org.slf4j:slf4j-simple:1.5.8'
    }

    war {
        from project(':applet').visu.outputs
        from project(':applet').dataserver.outputs

        def replaceTokens = {
            it.filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['portal_version': project.version])
        }
        filesMatching('**/portal.html', replaceTokens)
        filesMatching('**/scheduler.conf', replaceTokens)
    }
}

configure([project(':scheduler-portal'), project(':rm-portal')]) {
    apply plugin: 'war'
    task compileGwt(dependsOn: classes, type: JavaExec) {
        inputs.source sourceSets.main.java.srcDirs
        inputs.dir sourceSets.main.output.resourcesDir
        outputs.dir "$buildDir/portal"

        logging.captureStandardOutput LogLevel.INFO

        main = 'com.google.gwt.dev.Compiler'
        classpath {
            [sourceSets.main.java.srcDirs,           // Java source
                    sourceSets.main.output.resourcesDir,    // Generated resources
                    sourceSets.main.output.classesDir,      // Generated classes
                    sourceSets.main.compileClasspath,       // Deps
            ]
        }
        args = ["org.ow2.proactive_grid_cloud_portal.${project.name.capitalize() - "-portal"}",
                '-war', "$buildDir/portal",
                '-logLevel', 'INFO',
                '-localWorkers', '2',
                '-compileReport',
                '-extra', "$buildDir/portal",
                '-draftCompile' // Speeds up compile with 25%
        ]
        systemProperties = ['gwt.persistentunitcachedir': buildDir]

        jettyRun.dependsOn compileGwt
        war.dependsOn compileGwt
    }
    war {
        from "$buildDir/portal"
    }
}
